import Polyhedral.Toric.Mathlib.Geometry.Convex.Cone.Polyhedral
import Mathlib.LinearAlgebra.PerfectPairing.Basic

lemma ex_imp_of_imp_ex {Î± : Type*} [hÎ± : Nonempty Î±] {P : Prop} {Q : Î± â†’ Prop} :
    (P â†’ âˆƒ x, Q x) â†’ âˆƒ x, P â†’ Q x := by
  intro h
  by_cases hP : P
  Â· tauto
  Â· use Classical.choice hÎ±
    tauto

theorem Finset.exists_le'
    {Î± Î² : Type*} [Nonempty Î²] [Preorder Î²] [IsDirected Î² (Â· â‰¤ Â·)] [DecidableEq Î²]
    (s : Finset Î±) (f : Î± â†’ Î²) : âˆƒ M, âˆ€ i âˆˆ s, f i â‰¤ M := by
  let âŸ¨M, hMâŸ© := Finset.exists_le (image f s)
  use M
  simp only [mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚] at hM
  assumption

theorem Finset.exists_lt'
    {Î± Î² : Type*} [Nonempty Î²] [Preorder Î²] [IsDirected Î² (Â· â‰¤ Â·)] [NoMaxOrder Î²] [DecidableEq Î²]
    (s : Finset Î±) (f : Î± â†’ Î²) : âˆƒ M, âˆ€ i âˆˆ s, f i < M := by
  obtain âŸ¨M, hMâŸ© := Finset.exists_le' s f
  obtain âŸ¨N, hNâŸ© := exists_gt M
  use N
  exact fun i hi => lt_of_le_of_lt (hM i hi) hN

theorem eq_zero_of_nonneg_add
    {Î± : Type*} [AddGroup Î±] [PartialOrder Î±] [AddLeftMono Î±] {a b : Î±} :
    0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 0 â†’ a = 0 âˆ§ b = 0 := fun ha hb hab => by
  constructor
  Â· apply eq_neg_of_add_eq_zero_right at hab
    rw [hab, Left.nonneg_neg_iff] at hb
    exact le_antisymm hb ha
  Â· apply eq_neg_of_add_eq_zero_left at hab
    rw [hab, Left.nonneg_neg_iff] at ha
    exact le_antisymm ha hb


open Submodule Pointwise LinearMap

namespace PointedCone

section Definitions

variable {R M N : Type*} [CommRing R] [PartialOrder R] [IsOrderedRing R] [AddCommGroup M]
  [AddCommGroup N] [Module R M] [Module R N] {p : M â†’â‚—[R] N â†’â‚—[R] R}

variable (p) in
def perp (u : M) : PointedCone R N where
    carrier := {v | p u v = 0}
    add_mem' {v v'} hv hv' := by simp [Set.mem_setOf_eq â–¸ hv, Set.mem_setOf_eq â–¸ hv']
    zero_mem' := by simp
    smul_mem' c {v} hv := by simp [Set.mem_setOf_eq â–¸ hv]

@[simp] lemma mem_perp {u : M} {v : N} : v âˆˆ perp p u â†” p u v = 0 := by simp [perp, comm]
@[simp] lemma perp_zero : perp p 0 = âŠ¤ := by ext; simp

variable (p) in
def IsFace (c : PointedCone R N) (f : PointedCone R N) : Prop :=
  âˆƒ u âˆˆ dual p.flip c, f = c âŠ“ perp p u

lemma isFace_subset {c : PointedCone R N} {f : PointedCone R N} (h : IsFace p c f) : f â‰¤ c := by
  obtain âŸ¨hâ‚, âŸ¨hâ‚‚, hâ‚ƒâŸ©âŸ© := h
  simp [hâ‚ƒ]

-- TODO generalize to arbitrary (perfect?) pairing
theorem face_polyhedral
    {c : PointedCone R N} (hc : IsPolyhedral c)
    {f : PointedCone R N} (hf : IsFace .id c f) : IsPolyhedral f := by
  obtain âŸ¨S, hSâ‚, hSâ‚‚âŸ© := hc
  subst hSâ‚‚
  obtain âŸ¨u, huâ‚, huâ‚‚âŸ© := hf
  subst huâ‚‚
  use (insert (-u) S)
  apply And.intro (Set.Finite.insert _ hSâ‚)
  ext v
  rw [dual_insert, Submodule.mem_inf, Submodule.mem_inf]
  constructor
  Â· rintro âŸ¨hvâ‚, hvâ‚‚âŸ©
    apply And.intro hvâ‚‚ (le_antisymm _ _)
    Â· rwa [mem_dual'_singleton, map_neg, LinearMap.neg_apply, Left.nonneg_neg_iff] at hvâ‚
    Â· exact huâ‚ hvâ‚‚
  Â· intro âŸ¨hvâ‚, hvâ‚‚âŸ©
    apply And.intro _ hvâ‚
    simp only [mem_dual, Set.mem_singleton_iff, id_coe, id_eq, forall_eq, neg_apply,
      Left.nonneg_neg_iff]
    exact le_of_eq hvâ‚‚

theorem face_intersection
    {c : PointedCone R N}
    {f : PointedCone R N} (hf : IsFace p c f)
    {f' : PointedCone R N} (hf' : IsFace p c f') :
    IsFace p c (f âŠ“ f') := by
  obtain âŸ¨u, huâ‚, huâ‚‚âŸ© := hf
  subst huâ‚‚
  obtain âŸ¨u', hu'â‚, hu'â‚‚âŸ© := hf'
  subst hu'â‚‚
  use u + u'
  apply And.intro (add_mem huâ‚ hu'â‚)
  ext v
  constructor
  Â· intro hv
    obtain âŸ¨âŸ¨hvâ‚, hvâ‚‚âŸ©, âŸ¨-, hvâ‚ƒâŸ©âŸ© := hv
    apply And.intro hvâ‚
    rw [SetLike.mem_coe, mem_perp] at hvâ‚‚ hvâ‚ƒ
    simp [hvâ‚‚, hvâ‚ƒ]
  Â· intro âŸ¨hvâ‚, hvâ‚‚âŸ©
    rw [SetLike.mem_coe] at hvâ‚ hvâ‚‚
    specialize huâ‚ hvâ‚
    specialize hu'â‚ hvâ‚
    rw [LinearMap.flip_apply] at huâ‚ hu'â‚
    rw [mem_perp, map_add, LinearMap.add_apply] at hvâ‚‚
    obtain âŸ¨huv, hu'vâŸ© := eq_zero_of_nonneg_add huâ‚ hu'â‚ hvâ‚‚
    trivial

theorem add_mem_face
    {c : PointedCone R N}
    {f : PointedCone R N} (hf : IsFace p c f)
    {v : N} {hv : v âˆˆ c} {w : N} {hw : w âˆˆ c} :
    v + w âˆˆ f â†’ v âˆˆ f âˆ§ w âˆˆ f := fun h => by
  obtain âŸ¨u, huâ‚, huâ‚‚âŸ© := hf
  subst huâ‚‚
  obtain âŸ¨hâ‚, hâ‚‚âŸ© := h
  rw [SetLike.mem_coe, mem_perp, map_add] at hâ‚‚
  have âŸ¨huv, huwâŸ© := eq_zero_of_nonneg_add (huâ‚ hv) (huâ‚ hw) hâ‚‚
  trivial

end Definitions


section LinearOrder

variable {R M N : Type*} [CommRing R] [LinearOrder R] [IsStrictOrderedRing R] [AddCommGroup M]
  [AddCommGroup N] [Module R M] [Module R N] {p : M â†’â‚—[R] N â†’â‚—[R] R}

-- TODO do we need hR?
theorem face_face (hR : âˆ€ (x y : R), 0 < y â†’ âˆƒ z, 0 < z * y + x)
    {c : PointedCone R N} (hc : c.FG)
    {f : PointedCone R N} (hf : IsFace p c f)
    {f' : PointedCone R N} (hf' : IsFace p f f') :
    IsFace p c f' := by
  obtain âŸ¨u, huâ‚, huâ‚‚âŸ© := hf
  subst huâ‚‚
  obtain âŸ¨u', hu'â‚, hu'â‚‚âŸ© := hf'
  subst hu'â‚‚
  obtain âŸ¨S, hSâŸ© := hc
  have hSc : âˆ€ v âˆˆ S, v âˆˆ c := fun v hv => hS â–¸ subset_span hv
  obtain h_for_ex : âˆ€ v âˆˆ S, 0 < p u v â†’ âˆƒ r : R, 0 < p (r â€¢ u + u') v := by
    intro v hv huv
    obtain âŸ¨n, hnâŸ© := hR (p u' v) (p u v) huv
    use n
    simpa
  obtain âŸ¨rf, hrfâŸ© : âˆƒ (r : N â†’ R), âˆ€ v âˆˆ S, 0 < p u v â†’ 0 < p (r v â€¢ u + u') v := by
    apply Classical.axiomOfChoice (r := fun v r => _ â†’ _ â†’ 0 < p (r â€¢ u + u') v)
    exact fun _ => ex_imp_of_imp_ex (fun hv => ex_imp_of_imp_ex (h_for_ex _ hv))
  obtain âŸ¨r, hrâŸ© := Finset.exists_le' S rf
  have HS : âˆ€ v âˆˆ S, 0 â‰¤ p (r â€¢ u + u') v âˆ§
      (p (r â€¢ u + u') v = 0 â†” p u v = 0 âˆ§ p u' v = 0) := fun v hv => by
    have huv : 0 â‰¤ p u v := huâ‚ (hSc _ hv)
    rcases lt_or_eq_of_le huv with huvâ‚ | huvâ‚‚
    Â· have H : 0 < (p (r â€¢ u + u')) v := by
        apply lt_of_lt_of_le (hrf _ hv huvâ‚)
        simp [huvâ‚, hv, hr]
      apply And.intro (le_of_lt H)
      constructor <;> aesop
    Â· have hu'v : 0 â‰¤ p u' v := by tauto
      simp [â†huvâ‚‚, hu'v]
  have H : âˆ€ v âˆˆ c, 0 â‰¤ p (r â€¢ u + u') v âˆ§
      (p (r â€¢ u + u') v = 0 â†” p u v = 0 âˆ§ p u' v = 0) := fun v hv => by
    rw [iff_def, â†and_assoc]
    apply And.intro _ (by rintro âŸ¨huv, hu'vâŸ©; simp [huv, hu'v])
    obtain âŸ¨l, hl, hlvâŸ© := Submodule.mem_span_finset.mp (hS â–¸ hv)
    simp_rw [â†Nonneg.coe_smul] at hlv
    subst hlv
    simp_rw [map_sum, map_smul_of_tower]
    have hnn := fun w hw => smul_nonneg (l w).2 (HS w hw).1
    apply And.intro (Finset.sum_nonneg hnn)
    intro H
    have h_ruu'_zero := (Finset.sum_eq_zero_iff_of_nonneg hnn).mp H
    simp_rw [smul_eq_zero] at h_ruu'_zero
    have h_or_zero := fun i hi => Or.imp_right (HS i hi).2.mp (h_ruu'_zero i hi)
    simp_rw [or_and_left] at h_or_zero
    have hu_zero := fun i hi => smul_eq_zero.mpr (h_or_zero i hi).1
    have hu'_zero := fun i hi => smul_eq_zero.mpr (h_or_zero i hi).2
    constructor <;> apply Finset.sum_eq_zero <;> assumption
  use r â€¢ u + u'
  constructor
  Â· apply fun v hv => (H v hv).1
  Â· ext v
    simp_rw [mem_inf, mem_perp]
    have := fun hv => (H v hv).2
    tauto

end LinearOrder

end PointedCone


open PointedCone


section Definition

variable {R : Type*} [CommRing R] [PartialOrder R] [IsOrderedRing R]
  {N : Type*} [AddCommGroup N] [Module R N]

variable (R N) in
structure PolyhedralCone extends PointedCone R N where
  isPolyhedral : IsPolyhedral toSubmodule

namespace PolyhedralCone

attribute [coe] toSubmodule

instance : CoeOut (PolyhedralCone R N) (PointedCone R N) := âŸ¨toSubmoduleâŸ©

@[ext]
theorem ext {c c' : PolyhedralCone R N} (h : (c : PointedCone R N) = c') : c = c' :=
  by cases c; cases c'; congr

instance : SetLike (PolyhedralCone R N) N where
  coe s := s.carrier
  coe_injective' p q h := by cases p; cases q; congr; exact SetLike.coe_injective' h

end PolyhedralCone

end Definition


namespace PolyhedralCone

variable {ğ•œ N : Type*}
  [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup N] [Module ğ•œ N] [Module.Finite ğ•œ N]

-- TODO is this the most general M?
instance : LE (PolyhedralCone ğ•œ N) := {le := fun f f' => IsFace (M := Module.Dual ğ•œ N) .id f f'}

instance : PartialOrder (PolyhedralCone ğ•œ N) := {
  le_refl := by {intro a; use 0; constructor <;> simp}
  lt := fun a b => a â‰¤ b âˆ§ Â¬ b â‰¤ a
  le_trans := fun a b c ha hb => by {
    -- have hafg := fg_of_isPolyhedral id.bijective_left.injective
    --   hp.bijective_right.injective a.isPolyhedral
    have hR : âˆ€ (x y : ğ•œ), 0 < y â†’ âˆƒ z, 0 < z * y + x := fun x y hy => by
      use - (x - 1) / y
      field_simp
      simp
    exact face_face hR (fg_of_isPolyhedral a.isPolyhedral) ha hb
  }
  le_antisymm := fun a b hab hba => ext (antisymm (isFace_subset hba) (isFace_subset hab))
}

abbrev IsFacet (a b : PolyhedralCone ğ•œ N) := a â‹– b

end PolyhedralCone
