/-
Copyright (c) 2025 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.LinearAlgebra.Dual.Defs
import Mathlib.LinearAlgebra.PerfectPairing.Basic
import Mathlib.RingTheory.Finiteness.Basic
import Polyhedral.Toric.Mathlib.Geometry.Convex.Cone.Dual

/-!
# Polyhedral cones

Given a bilinear pairing `p` between two `R`-modules `M` and `N`, we define
polyhedral cones to be pointed cones in `N` that are the dual of a finite set
in `M` (this means they are the intersection of finitely many halfspaces).

The main statement is that if both `M` and `N` are finite and the pairing is injective
in both arguments, then polyhedral cones are precisely the finitely generated cones, see
`isPolyhedral_iff_fg`. Moreover, we obtain that the dual of a polyhedral cone is again polyhedral
(`IsPolyhedral.dual`) and that the double dual of a polyhedral cone is the cone itself
(`IsPolyhedral.dual_dual_flip`, `IsPolyhedral.dual_flip_dual`).
-/

open Function Module
open Submodule hiding span

variable {R ğ•œ M N : Type*}

local notation3 "Râ‰¥0" => {c : R // 0 â‰¤ c}
local notation3 "ğ•œâ‰¥0" => {c : ğ•œ // 0 â‰¤ c}

namespace PointedCone

section PartialOrder

variable [CommRing R] [PartialOrder R] [IsOrderedRing R] [AddCommGroup M] [AddCommGroup N]
  [Module R M] [Module R N] {p : M â†’â‚—[R] N â†’â‚—[R] R} [p.IsPerfPair] {C Câ‚ Câ‚‚ : PointedCone R N}
  {s : Set M}

/-- A cone is polyhedral if it is the dual of a finite set. -/
def IsPolyhedral (C : PointedCone R N) : Prop :=
  âˆƒ s : Set (Module.Dual R N), s.Finite âˆ§ dual .id s = C

variable (p) in
lemma isPolyhedral_iff_exists_finite_dual_eq :
    IsPolyhedral C â†” âˆƒ s : Set M, s.Finite âˆ§ dual p s = C := by
  refine (Equiv.Set.congr p.toPerfPair.symm.toEquiv).exists_congr fun s â†¦ ?_
  congr!
  Â· exact (Set.finite_image_iff p.toPerfPair.symm.injective.injOn).symm
  Â· ext
    simp

alias âŸ¨IsPolyhedral.exists_finite_dual_eq, _âŸ© := isPolyhedral_iff_exists_finite_dual_eq

lemma IsPolyhedral.dual_of_finite (hs : s.Finite) : IsPolyhedral (dual p s) :=
  (isPolyhedral_iff_exists_finite_dual_eq _).2 âŸ¨s, hs, rflâŸ©

lemma IsPolyhedral.dual_of_fg {C : PointedCone R M} (hC : C.FG) :
    IsPolyhedral (dual p (C : Set M)) := by
  obtain âŸ¨s, rflâŸ© := hC; rw [dual_span]; exact .dual_of_finite s.finite_toSet

variable (p) in
lemma IsPolyhedral_iff_exists_finset : IsPolyhedral C â†” âˆƒ s : Finset M, dual p s = C where
  mp hC := by obtain âŸ¨s, hs, rflâŸ© := hC.exists_finite_dual_eq p; exact âŸ¨hs.toFinset, by simpâŸ©
  mpr := by rintro âŸ¨s, rflâŸ©; exact .dual_of_finite s.finite_toSet

lemma IsPolyhedral.top : IsPolyhedral (âŠ¤ : PointedCone R N) := âŸ¨âˆ…, by simpâŸ©

variable (p) in
@[simp] lemma IsPolyhedral.dual_dual_flip (hC : IsPolyhedral C) :
    dual p (dual p.flip (C : Set N)) = C := by
  obtain âŸ¨s, hs, rflâŸ© := hC.exists_finite_dual_eq p; exact dual_dual_flip_dual _

variable (p) in
@[simp] lemma IsPolyhedral.dual_flip_dual {C : PointedCone R M} (hC : IsPolyhedral C) :
    dual p.flip (dual p (C : Set M)) = C := hC.dual_dual_flip _

lemma isPolyhedral.dual_inj (hCâ‚ : IsPolyhedral Câ‚) (hCâ‚‚ : IsPolyhedral Câ‚‚) :
    dual p.flip Câ‚ = dual p.flip Câ‚‚ â†” Câ‚ = Câ‚‚ where
  mp h := by rw [â† hCâ‚.dual_dual_flip p, â† hCâ‚‚.dual_dual_flip p, h]
  mpr h := by rw [h]

end PartialOrder

section LinearOrder
variable [CommRing R] [LinearOrder R] [IsOrderedRing R] [AddCommGroup M] [Module R M]
  [Projective R M]

/-- If the module `M` is finite, then the zero cone in `M` is polyhedral. -/
lemma IsPolyhedral.bot [Module.Finite R M] : IsPolyhedral (âŠ¥ : PointedCone R M) := by
  obtain âŸ¨s, hS : span R _ = âŠ¤âŸ© := Module.Finite.fg_top (R := Râ‰¥0) (M := Module.Dual R M)
  refine âŸ¨s, s.finite_toSet, ?_âŸ©
  rw [â† dual_span', hS, Submodule.top_coe, dual_univ, Submodule.zero_eq_bot]
  -- TODO: Rename to `Module.Dual.eval_injective`
  exact Module.eval_apply_injective _

end LinearOrder

section LinearOrder
variable [Field ğ•œ] [LinearOrder ğ•œ] [AddCommGroup M] [Module ğ•œ M] {s : Set (Dual ğ•œ M)} {w : M}

variable (s w) in
/-- A set whose dual cone is `span ğ•œ {w} âŠ” dual p s`, see `dual_sup_span_singleton_eq_dual` -/
private noncomputable abbrev auxGenSet : Set (Dual ğ•œ M) :=
  {x âˆˆ s | 0 â‰¤ x w} âˆª .image2 (fun x y â†¦ x w â€¢ y - y w â€¢ x) {x âˆˆ s | 0 â‰¤ x w} {y âˆˆ s | y w < 0}

private lemma auxGenSet_finite (hs : s.Finite) : (auxGenSet s w).Finite :=
  .union (hs.sep _) <| .image2 _ (hs.sep _) (hs.sep _)

variable [IsStrictOrderedRing ğ•œ] {C : PointedCone ğ•œ M}

private lemma auxGenSet_subset_span : auxGenSet s w âŠ† span ğ•œ s := by
  simp only [Set.union_subset_iff, Set.image2_subset_iff, Set.mem_setOf_eq, and_imp]
  refine âŸ¨subset_trans (fun x hx â†¦ hx.1) subset_span, fun x hxS hxw y hyS hyw â†¦ ?_âŸ©
  simpa [sub_eq_add_neg] using add_mem (smul_mem (span ğ•œ s) âŸ¨x w, hxwâŸ© (subset_span hyS))
    (smul_mem _ âŸ¨-y w, neg_nonneg.mpr hyw.leâŸ© (subset_span hxS))

private lemma span_singleton_le_dual_auxGenSet : span ğ•œ {w} â‰¤ dual .id (auxGenSet s w) := by
  simp only [span_singleton_le_iff_mem, mem_dual, Set.mem_union, Set.mem_setOf_eq, Set.mem_image2]
  rintro z (hz | âŸ¨x, âŸ¨hxS, hxwâŸ©, y, âŸ¨hyS, hywâŸ©, rflâŸ©)
  Â· exact hz.2
  Â· simp [mul_comm]

/-- The crucial lemma in the proof that a finitely generated cone is polyhedral:
The sum of a polyhedral cone and the cone generated by a single ray is again polyhedral. -/
private lemma dual_auxGenSet (hs : s.Finite) :
    dual .id (auxGenSet s w) = span ğ•œ {w} âŠ” dual .id s := by
  classical
  apply ge_antisymm
  Â· rw [â† dual_span]
    exact sup_le span_singleton_le_dual_auxGenSet <| dual_le_dual auxGenSet_subset_span
  obtain hSw | hSw := {y âˆˆ s | y w < 0}.eq_empty_or_nonempty
  Â· simp only [Set.sep_eq_empty_iff_mem_false, not_lt] at hSw
    exact le_sup_of_le_right <| dual_le_dual fun x hx => .inl âŸ¨hx, hSw _ hxâŸ©
  rw [dual_union]
  intro v âŸ¨hv1, hv2âŸ©
  rw [Submodule.mem_sup]
  replace hv2 {x y : Dual ğ•œ M} (hx : x âˆˆ s âˆ§ 0 â‰¤ x w) (hy : y âˆˆ s âˆ§ y w < 0) :
      y w * x v â‰¤ y v * x w := by
    simp only [SetLike.mem_coe, mem_dual, Set.mem_image2, Set.mem_setOf_eq,
      forall_exists_index, and_imp] at hv2
    specialize hv2 x hx.1 hx.2 y hy.1 hy.2 rfl
    simp only [map_sub, map_smul, LinearMap.sub_apply, LinearMap.smul_apply, smul_eq_mul,
      sub_nonneg] at hv2
    nth_rw 2 [mul_comm] at hv2
    exact hv2
  obtain hSv | âŸ¨y, hyâŸ© := {y âˆˆ s | y w < 0 âˆ§ y v < 0}.eq_empty_or_nonempty
  Â· simp +contextual only [Set.sep_and, Set.eq_empty_iff_forall_notMem, Set.mem_inter_iff,
      Set.mem_setOf_eq, not_and, true_and, not_lt, and_imp] at hSv
    refine âŸ¨0, zero_mem _, v, fun x hx => ?_, zero_add _âŸ©
    by_cases hxw : 0 â‰¤ x w
    Â· exact hv1 âŸ¨hx, hxwâŸ©
    Â· exact hSv x hx (lt_of_not_ge hxw)
  lift s to Finset (Dual ğ•œ M) using hs
  let u : ğ•œ := ({y âˆˆ s | y w < 0}.image (fun y => y v * (y w)â»Â¹)).max' <| by
    simpa [Finset.Nonempty, Set.Nonempty] using hSw.image _
  have hu : 0 â‰¤ u := by
    refine le_trans (mul_nonneg_of_nonpos_of_nonpos hy.2.2.le (inv_nonpos.mpr hy.2.1.le))
      (Finset.le_max' _ (y v * (y w)â»Â¹) ?_)
    simp only [Finset.mem_image, Finset.mem_filter]
    exact âŸ¨y, âŸ¨hy.1, hy.2.1âŸ©, rflâŸ©
  refine âŸ¨u â€¢ w, ?_, v - u â€¢ w, fun z hzS â†¦ ?_, add_sub_cancel _ _âŸ©
  Â· rw [â† Nonneg.mk_smul _ hu]
    exact Submodule.smul_mem _ _ (Submodule.subset_span rfl)
  simp only [map_sub, map_smul, smul_eq_mul, sub_nonneg]
  obtain hzw | hzw := lt_or_ge (z w) 0
  Â· dsimp
    rw [â† _root_.mul_le_mul_right_of_neg (inv_neg''.mpr hzw), mul_inv_cancel_rightâ‚€ hzw.ne]
    exact Finset.le_max' _ (z v * (z w)â»Â¹) <|
      Finset.mem_image.mpr âŸ¨z, Finset.mem_filter.mpr âŸ¨hzS, hzwâŸ©, rflâŸ©
  obtain âŸ¨y, hy, t_eq : _ = uâŸ© := Finset.mem_image.mp <|
    ({y âˆˆ s | y w < 0}.image (fun y => y v * (y w)â»Â¹)).max'_mem <| by
      simpa [Finset.Nonempty, Set.Nonempty] using hSw.image _
  rw [Finset.mem_filter] at hy
  rw [â† t_eq, â† _root_.mul_le_mul_left_of_neg hy.2, â† mul_assoc]
  nth_rw 4 [mul_comm]
  rw [mul_inv_cancel_leftâ‚€ hy.2.ne]
  exact hv2 âŸ¨hzS, hzwâŸ© hy

variable [AddCommGroup N] [Module ğ•œ N] {p : N â†’â‚—[ğ•œ] M â†’â‚—[ğ•œ] ğ•œ} [Module.Finite ğ•œ M] {s : Set M}

variable (ğ•œ) in
/-- A finitely generated cone is polyhedral. -/
lemma IsPolyhedral.of_fg (hC : C.FG) : IsPolyhedral C := by
  classical
  obtain âŸ¨s, rflâŸ© := hC
  induction s using Finset.induction with
  | empty =>
    rw [Finset.coe_empty, span_empty]
    exact .bot
  | @insert w A hwA hA =>
    obtain âŸ¨s, hs, hsAâŸ© := hA
    rw [Finset.coe_insert, Submodule.span_insert, â† hsA, â† dual_auxGenSet hs]
    exact âŸ¨_, auxGenSet_finite hs, rflâŸ©

protected lemma IsPolyhedral.span (hS : s.Finite) : IsPolyhedral (span ğ•œ s) := .of_fg ğ•œ (fg_span hS)

/-- The double dual of a finite set equals the cone generated by that set. -/
lemma dual_dual_eq_span [p.IsPerfPair] {s : Set M} (hS : s.Finite) :
    dual p (dual p.flip s) = span ğ•œ s := by
  simpa using IsPolyhedral.dual_dual_flip p (.span hS)

/-- A polyhedral cone is finitely generated. -/
lemma fg_of_isPolyhedral (hC : IsPolyhedral C) : C.FG := by
  obtain âŸ¨s, S_fin, rflâŸ© := hC
  obtain âŸ¨T, T_fin, hTâŸ© :=
    (IsPolyhedral.of_fg ğ•œ <| fg_span S_fin).exists_finite_dual_eq (Module.Dual.eval ğ•œ M)
  rw [â† dual_span', span, â† hT, Module.Dual.eval, dual_dual_eq_span T_fin]
  exact Submodule.fg_span T_fin

alias IsPolyhedral.fg := fg_of_isPolyhedral

/-- A cone is polyhedral if and only if it is finitely generated. -/
lemma isPolyhedral_iff_fg : IsPolyhedral C â†” C.FG := âŸ¨fg_of_isPolyhedral , .of_fg _âŸ©

/-- The dual of a polyhedral cone is again polyhedral. -/
protected lemma IsPolyhedral.dual [p.IsPerfPair] (hC : IsPolyhedral C) :
    IsPolyhedral (dual p.flip C) := .dual_of_fg (fg_of_isPolyhedral hC)

end LinearOrder
end PointedCone
